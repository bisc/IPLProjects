--AADL2
--SAE Aerospace Standard AS5506B
--Appendix A: Predeclared Property Sets

property set Communication_Properties is
	Fan_Out_Policy: enumeration (Broadcast, RoundRobin, Selective, OnDemand)
		applies to (feature);
	
	Connection_Pattern: list of list of Supported_Connection_Patterns
		applies to (connection);
	
	Connection_Set: list of Connection_Pair
		applies to (connection);
	
	Connection_Pair: type record (
		src: list of aadlinteger;
		dst: list of aadlinteger;);
	
	Overflow_Handling_Protocol: enumeration (DropOldest, DropNewest, Error) => DropOldest
		applies to (event port, event data port, subprogram access);
	
	Queue_Processing_Protocol: Supported_Queue_Processing_Protocols => FIFO
		applies to (event port, event data port, subprogram access);
	
	Queue_Size: aadlinteger 0 .. Max_Queue_Size => 1
		applies to (event port, event data port, subprogram access);
	
	Required_Connection: aadlboolean 
		applies to (feature);
	
	Timing: enumeration (sampled, immediate, delayed) => sampled
		applies to (port connection);
	
	Transmission_Type: enumeration (push, pull)
		applies to (feature, connection, bus, virtual bus);
	
	Input_Rate: Rate_Spec => [Value_Range => 1.0 .. 1.0; Rate_Unit => PerDispatch; Rate_Distribution => Fixed;]
		applies to (feature);
	
	Input_Time: list of IO_Time_Spec => ([Time => Dispatch; Offset => 0 ns .. 0 ns;])
		applies to (feature);
	
	IO_Time_Spec: type record (
		Offset: Time_Range;
		Time: IO_Reference_Time;
	);
	
	IO_Reference_Time: type enumeration (Dispatch, Start, Completion, Deadline, NoIO, Dynamic);
	
	Output_Rate: Rate_Spec => [Value_Range => 1.0 .. 1.0; Rate_Unit => PerDispatch; Rate_Distribution => Fixed;]
		applies to (feature);
	
	Output_Time: list of IO_Time_Spec => ([Time => Completion; Offset => 0 ns .. 0 ns;])
		applies to (feature);
	
	Rate_Spec: type record (
		Value_Range: range of aadlreal;
		Rate_Unit: enumeration (PerSecond, PerDispatch);
		Rate_Distribution: Supported_Distributions;
	);
	
	Subprogram_Call_Rate: Rate_Spec => [Value_Range => 1.0 .. 1.0; Rate_Unit => PerDispatch; Rate_Distribution => Fixed;]
		applies to (subprogram access);
	
	Transmission_Time: record (
			Fixed: Time_Range;
			PerByte: Time_Range;)
		applies to (bus,system,device,processor,memory,virtual bus, virtual processor);
	
	Actual_Latency: Time_Range
		applies to (flow, connection, virtual bus, bus, processor, virtual processor, device, system, feature, memory);
	
	Latency: Time_Range
		applies to (flow, connection, virtual bus, bus, processor, virtual processor, device, system, feature, memory);
		
	Data_Rate : aadlinteger units Data_Rate_Units applies to (feature, connection, bus, virtual bus, system, processor, virtual processor, memory, device);

end Communication_Properties;